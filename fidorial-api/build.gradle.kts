import fr.euphyllia.fidorial.gradle.libraries.GenerateApiPackageIndexTask

plugins {
    `maven-publish`
    id("fidorial-spotless")
    id("fidorial-build-conventions")
    id("fr.fidorial.registry-generator")
}

dependencies {
    api(libs.adventure.text.serializer.ansi)
    api(libs.adventure.text.serializer.plain)
    api(libs.brigadier)
    api(libs.bundles.adventure)
    api(libs.gson)
    api(libs.guava)
    api(libs.jspecify)
    api(libs.slf4j.api)
    api(platform(libs.adventure.bom))
    api(libs.fastutil)
    api(libs.configurate.yaml)
    compileOnly(libs.jetbrains.annotations)
}

fidorialBuild {
    readUnnamedModules = setOf("fr.fidorial")
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceSets.main {
        java.srcDirs(layout.projectDirectory.dir("src/generated/java"))
        resources.srcDirs(layout.projectDirectory.dir("src/generated/resources"))
    }
}

tasks.javadoc {
    val adventureVersion =
        libs.versions.adventure
            .asProvider()
            .get()
    val slf4jVersion = libs.versions.slf4j.get()
    val guavaVersion = libs.versions.guava.get()
    val gsonVersion = libs.versions.gson.get()
    val jetbrainsAnnotationsVersion =
        libs.versions.jetbrains.annotations
            .get()

    val opt = options as StandardJavadocDocletOptions

    opt.encoding = "UTF-8"
    opt.docEncoding = "UTF-8"
    opt.charSet = "UTF-8"
    opt.docTitle = "Fidorial API ${project.version}"
    opt.windowTitle = "fidorial-api ${project.version}"
    opt.addBooleanOption("html5", true)
    opt.noTimestamp(true)
    opt.addStringOption("Xdoclint:all,-missing", "-quiet")
    opt.tags("apiNote:a:API Note:", "sinceMinecraft:a:Since Minecraft:")

    opt.links(
        "https://docs.oracle.com/en/java/javase/25/docs/api/",
        "https://jd.papermc.io/adventure/$adventureVersion/",
        "https://www.javadocs.dev/org.slf4j/slf4j-api/$slf4jVersion",
        "https://guava.dev/releases/$guavaVersion/api/docs/",
        "https://www.javadocs.dev/com.google.code.gson/gson/$gsonVersion",
        "https://www.javadocs.dev/org.jetbrains/annotations/$jetbrainsAnnotationsVersion/",
        "https://jspecify.dev/docs/api/",
    )

    opt.bottom(
        "MIT © 2026 Euphyllia Bierque — " +
            "<a href=\"https://github.com/Euphillya/Fidorial\">GitHub</a>",
    )
}

val apiResolvable =
    configurations.resolvable("apiResolvable") {
        extendsFrom(configurations.api)
    }

val generateApiPackageIndex =
    tasks.register<GenerateApiPackageIndexTask>("generateApiPackageIndex") {
        group = "build"
        description = "Records which packages plugins must always load from the server."
        apiSurface.from(tasks.jar, apiResolvable)
        outputDirectory.set(layout.buildDirectory.dir("generated/fidorial-api-index"))
    }

configurations.consumable("apiSurfaceIndex") {
    description = "Packages the server must load parent-first for plugins"
    attributes {
        attribute(Category.CATEGORY_ATTRIBUTE, named(GenerateApiPackageIndexTask.API_PACKAGE_INDEX_CATEGORY))
    }
    outgoing.artifact(generateApiPackageIndex.flatMap { it.outputDirectory }) {
        type = ArtifactTypeDefinition.DIRECTORY_TYPE
    }
}

publishing {
    publications {
        register<MavenPublication>("maven") {
            from(components["java"])
            pom {
                name = "fidorial-api"
                description = "Plugin API for the Fidorial Minecraft server"
                url = "https://repo.euphyllia.moe"
            }
        }
    }
    repositories {
        maven {
            name = "Euphyllia"
            val releases = uri("https://repo.euphyllia.moe/repository/maven-releases/")
            val snapshots = uri("https://repo.euphyllia.moe/repository/maven-snapshots/")
            url = if (version.toString().endsWith("SNAPSHOT")) snapshots else releases
            credentials {
                username = providers.environmentVariable("NEXUS_USERNAME").orNull ?: ""
                password = providers.environmentVariable("NEXUS_PASSWORD").orNull ?: ""
            }
        }
    }
}

val verifyRegistryDataset =
    tasks.register("verifyRegistryDataset") {
        group = "verification"
        description = "Fails if the generated registry dataset is missing from the packaged resources."

        dependsOn(tasks.processResources)

        val datasetDirectory =
            sourceSets.main
                .get()
                .output.resourcesDir!!
                .resolve("fidorial-data")
        val expected = listOf("registries_dynamic.json")

        doLast {
            val missing = expected.filter { datasetDirectory.resolve(it).length() <= 2 }
            if (missing.isNotEmpty()) {
                throw GradleException(
                    "Registry dataset missing or empty: ${missing.joinToString()}. " +
                        "Run ':fidorial-api:generateRegistries'.",
                )
            }
        }
    }

tasks.named("check") { dependsOn(verifyRegistryDataset) }

fidorialRegistryGenerator {
    minecraftVersion.set(providers.gradleProperty("minecraftVersion"))

    generatedSourcesDirectory.set(
        layout.projectDirectory.dir(
            "src/generated/java",
        ),
    )

    // "--server" additionally dumps the vanilla tag files the dataset is built from.
    dataGeneratorArguments.set(
        listOf("--reports", "--server"),
    )

    registriesDatasetDirectory.set(
        layout.projectDirectory.dir(
            "src/generated/resources/fidorial-data",
        ),
    )

    // FrozenRegistries is generated once, in fidorial-server, where the runtime reads it.
    frozenRegistries.set(emptyList())
}
